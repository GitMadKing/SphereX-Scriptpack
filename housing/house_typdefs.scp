//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

[TYPEDEF t_house_lockdown]
ON=@Destroy // When destroyed, the item must be unlinked from the house.
if (<Link>)
    if (<Link.Type>==t_multi || <Link.Type>==t_ship || <Link.Type>==t_multi_custom)
        Link.UnlockItem <uid>
    endif
endif

ON=@ClientToolTip
src.addcliloc 501643
 
ON=@PickUp_Ground   // Do not allow to move this item
if (!<src.IsGM>)
    return 1
endif
 
ON=@PickUp_Self // For containers only, do not allow to pick up items
if (!<src.IsGM>)
    return 1
endif
 
// When entering customize mode, all locked down items and containers are being moved to the Moving Crate
// but they are still Locked Down items, counting as so and having their properties
// so they can't be moved from ground nor items can be dragged from them
// but they can still be picked up from the moving crate and dropped on the ground or other containers
// that's why some checks must be done to prevent malintentionated/unintended placements outside the multi.
ON=@DropOn_Ground
if (<region.uid> != <link.region.uid>)
    return 1
endif
 
ON=@DropOn_Item
if (<argo.region.uid> != <link.region.uid>)
    return 1
endif
 
[TYPEDEF t_house_door]
ON=@Click	
if (<tag0.guild>)
	message @,,1 [<uid.<tag0.guild>.abbrev>] Guild Members Only.
	message @,,1 <name>
	return 1
elif (<tag0.owneronly>)
	message @,,1 Owner Only
	message @,,1 <name>
	return 1
elif (<tag0.restricted>==1)
	message @,,1 Co-Owners Only
	message @,,1 <name>
	return 1
elif (<tag0.restricted>==2)
	message @,,1 Friends Only
	message @,,1 <name>
	return 1
endif

ON=@ClientToolTip
if (<tag0.guild>)
	src.addcliloc 1042971, [<uid.<tag0.guild>.abbrev>] Guild Members Only
	return 1
elif (<tag0.owneronly>)
	src.addcliloc 1070722, Private
	src.addcliloc 1042971, Owner Only
	return 1
elif (<tag0.restricted>)
	if (<tag0.restricted>==1)
		src.addcliloc 1042971, Restricted Access
		src.addcliloc 1070722, Owner & Co-owners Only
		return 1
	elif (<tag0.restricted>==2)
		src.addcliloc 1042971, Restricted Access
		src.addcliloc 1070722, Owner Co-owners & Friends Only
		return 1
	endif
endif

ON=@DClick
type=t_door
ref1=<region.uid>
if <def.can_decay> 
	if (<ref1.tag0.decay_exempt>)
		ref1.link.timer=-1
	else
		if !<def.property_tax>
			if (<ref1.isowner <src>>)
				if !(<ref1.tag0.demolition>)
					ref1.link.timer=60*60*24*<def.can_decay>
				else
					src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
					src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
				endif
			endif
		else
			if (<ref1.tag0.demolition>)
				src.sysmessage @32,,1 This house is improperly placed and has been scheduled for demolition in  <QVAL (<eval <ref1.link.timer>/86400> < 1)? <eval (<ref1.link.timer>%86400)/3600> hours and <eval ((<ref1.link.timer>%86400)%3600)/60> minutes.:<eval <ref1.link.timer>/86400> days.>
				src.sysmessage @32,,1 Unless you redeed it yourself before that time the house and all items within will be lost without refund.
			endif
		endif
	endif
endif
if (<serv.AutoHouseKeys>)   // If keys are used, let the server check them.
    return 0
endif
if !(<src.isgm>)
	if (<f_house_can_access>)
		return 0
	endif
	return 1
endif

[TYPEDEF t_house_container]
ON=@DClick
ON=@PickUp_Self
ON=@PickUp_Ground
 
[TYPEDEF t_house_component]
ON=@Destroy
link.DelComponent=<uid> // Remove the component to the house list of components.
 
[TYPEDEF t_moving_crate]
ON=@DropOn_Self // Moving Crates are not meant for storage, no items can be dropped inside.
if !<src.isgm>
return 1
endif

ON=@PickUp_Self
if <count> <= 1
	remove
endif
 
[TYPEDEF t_house_secure]
ON=@Destroy // When destroyed, the item must be unlinked from the house.
if (<Link>)
    if (<Link.Type>==t_multi || <Link.Type>==t_ship || <Link.Type>==t_multi_custom)
        Link.Release <uid>
    endif
endif

on=@clienttooltip
src.addcliloc 501644
 
ON=@PickUp_ground
if (!<src.IsGM>)
    return 1
endif

on=@DClick
if (<f_house_can_access>)
    return 0
endif
return 1
 
ON=@PickUp_Self
if (<f_house_can_access>)
    return 0
endif
return 1

// [typedef t_weapon_axe]
// ON=@TargOn_Item
// if strmatch("t_multi*","<uid.<src.region.uid>.type>")
	// ref1=<src.region.tag0.sign>
	// ref2=<argo.link>
	// if <ref2>
		// if <ref2.<uid.<ref2.region.uid>.tag0.sign>==<ref1.link>

[typedef t_house_telepad]
ON=@Click	
if (<tag0.guild>)
	message @,,1 [<uid.<tag0.guild>.abbrev>] Guild Members Only.
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_owner)
	message @,,1 Owner Only
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_coowner)
	message @,,1 Co-Owners Only
	message @,,1 <name>
	return 1
elif (<tag0.access>==house_access_friend)
	message @,,1 Friends Only
	message @,,1 <name>
	return 1
endif

ON=@ClientToolTip
if (<tag0.guild>)
	src.addcliloc 1042971, [<uid.<tag0.guild>.abbrev>] Guild Members Only
	return 1
elif (<tag0.access>==house_access_owner)
	src.addcliloc 1070722, Private
	src.addcliloc 1042971, Owner Only
	return 1
elif (<tag0.access>==house_access_coowner)
	src.addcliloc 1042971, Restricted Access
	src.addcliloc 1070722, Owner & Co-owners Only
	return 1
elif (<tag0.access>==house_access_friend)
	src.addcliloc 1042971, Restricted Access
	src.addcliloc 1070722, Owner Co-owners & Friends Only
	return 1
endif


on @step
if !(<link>==04fffffff) 
	if !(<src.isgm>)
		if (<f_house_can_access>)
			return 0
		endif
		return 1
	endif
endif

[TYPEDEF t_trade_house_deed]
ON=@Click
MESSAGE House Transfer Contract
MESSAGE House Name: <LINK.NAME>
MESSAGE Owner: <UID.<LINK.MORE1>.NAME>
MESSAGE Location: <LINK.SEXTANTP> //<LINK.REGION.REGION.NAME> (<LINK.P>)
return 1

ON=@ClientToolTip
SRC.ADDCLILOC 1061112,<LINK.NAME>
SRC.ADDCLILOC 1061113,<UID.<LINK.MORE2>.NAME>
SRC.ADDCLILOC 1061114,<STRARG <LINK.SEXTANTP>> <STREAT <LINK.SEXTANTP>>

ON=@Timer
if (<CONT.TYPE>==t_eq_trade_window)
	TIMERF 2,trigger @Timer
else
	UID.<MORE1>.EVENTS=-e_house_transfer
	REMOVE
endif
return 1

ON=@HouseTraded
REF1=<MORE1>		//New Owner
REF2=<LINK.owner>	//Old Owner
REF1.EVENTS=-e_house_transfer
ref2.events=-e_house_transfer
LINK.TAG.lasttransfer=<serv.rtime>
ref2.delhouse <uid>
LINK.owner=<REF1>
for <eval <REF2.COUNT>-1> 0
	if (<REF2.FINDCONT.<LOCAL._FOR>.LINK>==<LINK.LINK>)
		REF2.FINDCONT.<dLOCAL._FOR>.CONT=<REF1>
	endif
endfor
REF2.SYSMESSAGE @,,1 You transfer the property to <ref1.name>
ref2.sysmessage @,,1 The number of houses you own is now <ref2.houses>.
REF1.SYSMESSAGE @,,1 <ref2.name> has transferred ownership of this property to you.
ref1.sysmessage @,,1 The number of houses you own is now <ref1.houses>.
link.delban -1
link.delcoowner -1
link.delaccess -1
link.delfriend -1
link.resendtooltip 1

[TYPEDEF t_deed]
ON=@Targon_Ground

[TYPEDEF t_owneronly]
ON=@Click
	message @,,1 Owner's Storage
ON=@DClick
	ref1 = <region.uid>
	if !<src.isgm>
		if (<ref1.isowner <src>>)
			return 0
		else
			src.sysmessage @32,,1 This <name> belongs to Owner you cannot open it.
			return 1
		endif
	endif

ON=@ClientToolTip
	src.addcliloc 1042971,Owner Only
	src.addcliloc 501644 

[TYPEDEF t_coowner]
ON=@Click
	message @,,1 Owner & Co-owner Storage
ON=@DClick
ref1 = <region.uid>
	if !<src.isgm>
		if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0)
			return 0
		else
			src.sysmessage @32,,1 Only the owner & co-owners can open this <name>.
			return 1
		endif
	endif

ON=@ClientToolTip
	src.addcliloc 1042971,Owner & Co-owners Only
	src.addcliloc 501644 

[TYPEDEF t_friend]
On=@Click
	message @,,1 Owner, Co-owner & Friend's Chest
ON=@DClick
ref1= <region.uid>
	if !<src.isgm>
		if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >=0)
			return 0
		else
			src.sysmessage @32,,1 Only The owner, co-owners & friends of the house can open this <name>.
			return 1
		endif
	endif

ON=@ClientToolTip
	src.addcliloc 1042971,Owner Co-Owners & Friends Only
	src.addcliloc 501644 

[TYPEDEF t_guild]
ON=@Click
	ref1=<src.region.uid>
	message @,,1 <uid.<ref1.guild>.abbrev> guild chest
ON=@DClick
	if !<src.isgm>
		ref1=<src.region.uid>	
			if (<src.guild>=!<ref1.guild>)
				src.sysmessage @32,,1 Only <uid.<ref1.guild>.abbrev> guild members can open this <name>.
				return 1
			else
				return 0
			endif
	endif

ON=@ClientToolTip
ref1=<src.region.uid>
	src.addcliloc 1042971,[<uid.<ref1.guild>.abbrev>]
	src.addcliloc 501644 

[REGIONTYPE r_house_system]
ON=@Enter
if <src.isplayer>
	src.events +e_house_player_events
	SRC.SPEECH +spk_player_house
	src.timerf 1, visitor_count
	ref1=<uid>
	if !<src.isgm>
		if <ref1.housetype>==house_private
			if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<src.isgm>) 
				return 0
			elif (<ref1.housetype>==house_guild)
				if (<src.guild>==<ref1.tag0.is_guild>)
					return 0
				else
					src.sysmessage @32,,1 You are not a member of this guild
					return 1
				endif
			else
				src.sysmessage @32,,1 This is private property.
				src.sysmessage @32,,1 You may not trespass.
				return 1
			endif
		else
			if <ref1.getbanpos <src>> >= 0
				src.sysmessage @32,,1 You are banned from this property
				return 1
			else
				return 0
			endif
		endif
	endif
endif

ON=@Exit	
	src.dialogclose d_house_menu 
	src.events -e_house_player_events	


//////////////////////////////////////////////////////////////////////////////////////events///////////////////////////////////////////////////
[EVENTS e_house_transfer]
ON=@TradeAccepted
DIALOGCLOSE d_demolish
DIALOGCLOSE d_house_menu
if <argn1>
	for <ARGN1>
		if <REF<dLOCAL._FOR>>
			if (<REF<dLOCAL._FOR>.TYPE>==t_trade_house_deed)
				UID.<REF<dLOCAL._FOR>>.TRIGGER @HouseTraded
				UID.<REF<dLOCAL._FOR>>.REMOVE
			endif
		endif
	endfor
endif
EVENTS=-e_house_transfer

[EVENTS e_house_player_events]
ON=@ItemDClick
if (<act.defname>==<def.vendor_deed_id>)
	ref1=<region.uid>
	if !<def.house_list_vendors>
		if <ref1.isowner <uid>> || (<src.isgm)
			if <ref1.housetype>==house_private
				src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
				return 1
			else 
				if (<ref1.vendors> < <ref1.maxvendors>)
					return 0
				else
					sysmessage @32,,1 You have no available vendor slots
					sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
					return 1
				endif
			endif
		else
			sysmessage @32,,1 Only the home owner may place vendors here.
			return 1
		endif
	else
		if (<ref1.isowner <src>>) || (<ref1.getcoownerpos <src>> >= 0) || (<ref1.getfriendpos <src>> >= 0) || (<src.isgm)
			if <ref1.housetype>==house_private
				src.sysmessage @32,,1 You must set your building to public before you can place vendors here.
				return 1
			else 		
				if (<ref1.vendors> < <ref1.maxvendors>)
					return 0
				else
					sysmessage @32,,1 You have no available vendor slots
					sysmessage @32,,1 You must remove an existing vendor before you can place a new one.
					return 1
				endif
			endif
		else
			sysmessage @32,,1 Only people on the home lists may place a vendor here.
			return 1
		endif
	endif
endif

ON=@ContextMenuRequest
ref1 = <region.uid>
if <uid>==<src> && <ref1.link>
	src.addcontextentry 101,3006207
endif

ON=@ContextMenuSelect
if <argn>==101
	ref1 = <region.uid>
	src.go <uid.<ref1.link>.p>
endif

[EVENTS e_house_customize]
ON=@SkillStart
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	SYSMESSAGE @32,,1 You cannot do this whilst designing a house.
	RETURN 1

ON=@SkillUseQuick
	IF !(<HOUSEDESIGN>)
		RETURN 2
	ENDIF
	RETURN 1

// on=@HouseDesignCommitItem
// serv.log <local.id> --- <local.p.x> ---- <local.p.y> ------ <local.p.z> ------- <local.visible>

ON=@HouseDesignCommit
	IF (<ISGM>)
		SYSMESSAGE @,,1 Your new house design has been committed.
		RETURN 2
	ENDIF
	LOCAL.OLDCOST = <EVAL (<ARGN1> * 500)>
	LOCAL.NEWCOST = <EVAL (<ARGN2> * 500)>
	LOCAL.CURCOST = <EVAL (<LOCAL.NEWCOST> - <LOCAL.OLDCOST>)>
	IF (<GOLD> < <LOCAL.CURCOST>)
		SYSMESSAGE @32,,1 You cannot afford this house design.
		RETURN 1
	ENDIF
	argo.tag0.construction=<eval <local.newcost>-<argo.value>>
	SYSMESSAGE @,,1 Your new house design has been committed.
	IF (<LOCAL.CURCOST> == 0)
		SYSMESSAGE @,,1 As the new design costs the same as the previous one, no gold has been taken out of your account.
	ELSEIF (<LOCAL.CURCOST> < 0)
		LOCAL.CURCOST = <EVAL (0 - <LOCAL.CURCOST>)>
		GOLD += <LOCAL.CURCOST>
		SYSMESSAGE @,,1 As the new design is cheaper than the previous one, <dLOCAL.CURCOST> gold has been returned to you.
	ELSE
		GOLD -= <LOCAL.CURCOST>
		SYSMESSAGE @,,1 <dLOCAL.CURCOST> gold has been taken out of your account to pay for the construction.
	ENDIF
	RETURN 2

ON=@HouseDesignExit
	SYSMESSAGE @,,1 You have left house design mode.
	events -e_house_customize
	forplayers 20
	update
	endfor
	RETURN 2
