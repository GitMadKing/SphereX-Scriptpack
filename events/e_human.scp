//****************************************************************************
// SPHERE by : Menasoft Â©1997-2007
// www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Sunday, October 6, 2013
//
VERSION=0.56d

[EVENTS e_Human_Environ]
ON=@EnvironChange
	IF (<flags> & statf_war)
		return 0
	ENDIF
	IF !(<sector.isdark>) || (<flags> & statf_nightsight)
		IF (<findlayer(layer_hand2)>)
			IF (<findlayer(layer_hand2).type> == t_light_lit)
				findlayer(layer_hand2).bounce
			ENDIF
		ENDIF
		return 0
	ENDIF
	// already have a lit light ?
	IF (<findlayer(layer_hand2)>)
		IF (<findlayer(layer_hand2).type> == t_light_lit)
			return 0
		ENDIF
	ENDIF
	// try to use a torch or light source if i have one. (and it's dark)
	IF (<findtype.t_light_out>)
		findtype.t_light_out.equip
		findtype.t_light_out.use
	ENDIF
	return 0

[EVENTS e_trainer]
ON=@ContextMenuRequest
   IF (<NPC> < brain_human) || (<NPC> > brain_animal_trainer) || (<NPC> == brain_guard)
      return 0
   ENDIF
   FOR 0 57
      IF (<SERV.SKILL.<LOCAL._FOR>>)
         LOCAL.NpcSkill = <<SERV.SKILL.<LOCAL._FOR>.KEY>>
         IF (<LOCAL.NpcSkill>)
            LOCAL.PlayerSkill = <SRC.<SERV.SKILL.<LOCAL._FOR>.KEY>>
            LOCAL.Enabled = <QVAL ((<LOCAL.PlayerSkill> < <SERV.NPCTrainMax>) && (<eval (<LOCAL.NpcSkill> * <SERV.NPCTrainPercent>) / 100> > <LOCAL.PlayerSkill>)) ? 0 : 1>
            SRC.ADDCONTEXTENTRY <eval 200 + <LOCAL._FOR>>,<eval 6000 + <LOCAL._FOR>>,<LOCAL.Enabled>
         ENDIF
      ENDIF
   ENDFOR
ON=@ContextMenuSelect
   IF (<ARGN> >= 200) && (<ARGN> <= 257)
      TRAIN <SERV.SKILL.<eval <ARGN>-200>.KEY>
   ENDIF
   
   [EOF]
   